{
  parserClass="graphql.GraphQLParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="GraphQL"
  psiImplClassSuffix="Impl"
  psiPackage="graphql"
  psiImplPackage="graphql.impl"

  elementTypeHolderClass="graphql.GraphQLTypes"
  elementTypeClass="graphql.GraphQLElementType"
  tokenTypeClass="graphql.GraphQLTokenType"

  tokens = [
    STRING="regexp:\"[^\"]*\"|'[^']*'"
    NUMBER="regexp:(\+|\-)?\p{Digit}*"
    ID="regexp:\p{Alpha}\w*"

    COMMA=","
    COLON=":"
    EQUALS="="
    BRACE1="{"
    BRACE2="}"
    PAREN1="("
    PAREN2=")"
    BRACKET1="["
    BRACKET2="]"
    AT="@"
    DOLLAR="$"
    BANG="!"

    TYPE="type"
    IMPLEMENTS="implements"
    QUERY="query"
    MUTATION="mutation"
    FRAGMENT="fragment"
    SPREAD="..."
    ON="on"
    TRUE="true"
    FALSE="false"
  ]
}

graphqlFile ::= definition*

definition ::= type_definition | operation_definition | fragment_definition

type_definition ::= 'type' named_type interfaces? field_definitions {pin=1}
interfaces ::= 'implements' named_type
field_definitions ::= '{' field_definition (',' field_definition)* ','? '}'
  {pin=1}
field_definition ::= field_name ':' named_type '!'?

operation_definition ::=  selection_set |
  operation_type operation_name? variable_definitions? directive? selection_set
  {pin(".*")=1}
operation_name ::= ID
operation_type ::= 'query' | 'mutation'

selection_set ::= '{' selection (','? selection)* ','? '}' {pin=1}
selection ::= field | fragment_spread | inline_fragment
field ::= alias? field_name arguments? directives? selection_set? {pin=2}
field_name ::= ID

arguments ::= '(' argument (','? argument)* ','? ')' {pin=1}
argument ::= argument_name ':' argument_value {pin=2}
argument_name ::= ID
argument_value ::= value

alias ::= alias_name ':'
alias_name ::= ID

fragment_spread ::= '...' fragment_name directives?
fragment_definition ::= 'fragment' fragment_name? type_condition directives?
  selection_set
fragment_name ::= ID
inline_fragment ::= '...' type_condition? directives? selection_set

type_condition ::= 'on' named_type

value ::= variable | NUMBER | string_value |
  boolean_value | enum_value | list_value | object_value
string_value ::= '"' STRING '"'
boolean_value ::= 'true' | 'false'
private enum_value ::= STRING
list_value ::= '[' value (','? value)* ','? ']' {pin=1}
object_value ::= '{' object_field* '}'
object_field ::= field_name ':' value

variable_definitions ::= '(' variable_definition (','? variable_definition)* ','? ')'
  {pin=1}
variable_definition ::= variable ':' type_ref default_value? {pin=1}
variable ::= '$' var_name {pin=1}
var_name ::= ID
default_value ::= '=' value

type_ref ::= named_type | list_type //TODO: NonNullTypes
named_type ::= ID
list_type ::= '[' type_ref ']'

directives ::= directive*
directive ::= '@' directive_name arguments?
directive_name ::= ID